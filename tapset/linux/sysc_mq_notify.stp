# mq_notify __________________________________________________
# long sys_mq_notify(mqd_t mqdes, const struct sigevent __user *u_notification)
# long compat_sys_mq_notify(mqd_t mqdes, const struct compat_sigevent __user *u_notification)
#

@define _SYSCALL_MQ_NOTIFY_NAME
%(
	name = "mq_notify"
%)

@define _SYSCALL_MQ_NOTIFY_ARGSTR
%(
	argstr = sprintf("%d, %p", mqdes, notification_uaddr)
%)

@define _SYSCALL_MQ_NOTIFY_REGARGS
%(
	mqdes = int_arg(1)
	notification_uaddr = pointer_arg(2)
%)

probe syscall.mq_notify = dw_syscall.mq_notify !, nd_syscall.mq_notify ? {}
probe syscall.mq_notify.return = dw_syscall.mq_notify.return !,
                                 nd_syscall.mq_notify.return ? {}

# dw_mq_notify _____________________________________________________

probe dw_syscall.mq_notify =
        __syscall.mq_notify,
        kernel.function("compat_sys_mq_notify").call ?
{
	@_SYSCALL_MQ_NOTIFY_NAME
	mqdes = __int32($mqdes)
	notification_uaddr = @__pointer($u_notification)
	@_SYSCALL_MQ_NOTIFY_ARGSTR
}
probe __syscall.mq_notify = kernel.function("sys_mq_notify").call
{
        @__syscall_gate(@const("__NR_mq_notify"))
}
probe dw_syscall.mq_notify.return =
        __syscall.mq_notify.return,
        kernel.function("compat_sys_mq_notify").return ?
{
	@_SYSCALL_MQ_NOTIFY_NAME
	@SYSC_RETVALSTR($return)
}
probe __syscall.mq_notify.return = kernel.function("sys_mq_notify").return
{
        @__syscall_gate(@const("__NR_mq_notify"))
}

# nd_mq_notify _____________________________________________________

probe nd_syscall.mq_notify = nd1_syscall.mq_notify!, nd2_syscall.mq_notify!, tp_syscall.mq_notify
  { }

probe nd1_syscall.mq_notify =
        __nd1_syscall.mq_notify ?,
        kprobe.function("compat_sys_mq_notify") ?
{
	@_SYSCALL_MQ_NOTIFY_NAME
	asmlinkage()
	@_SYSCALL_MQ_NOTIFY_REGARGS
	@_SYSCALL_MQ_NOTIFY_ARGSTR
}
probe __nd1_syscall.mq_notify = kprobe.function("sys_mq_notify")
{
        @__syscall_gate(@const("__NR_mq_notify"))
}

/* kernel 4.17+ */
probe nd2_syscall.mq_notify =
	kprobe.function(@arch_syscall_prefix "sys_mq_notify") ?,
	kprobe.function(@arch_syscall_prefix "compat_sys_mq_notify") ?
{
	__set_syscall_pt_regs(pointer_arg(1))
	@_SYSCALL_MQ_NOTIFY_NAME
	@_SYSCALL_MQ_NOTIFY_REGARGS
	@_SYSCALL_MQ_NOTIFY_ARGSTR
}

/* kernel 3.5+, but undesirable because it affects all syscalls */
probe tp_syscall.mq_notify = kernel.trace("sys_enter")
{
	__set_syscall_pt_regs($regs)
	@__syscall_compat_gate(@const("__NR_mq_notify"), @const("__NR_compat_mq_notify"))
	@_SYSCALL_MQ_NOTIFY_NAME
	@_SYSCALL_MQ_NOTIFY_REGARGS
	@_SYSCALL_MQ_NOTIFY_ARGSTR
}

probe nd_syscall.mq_notify.return = nd1_syscall.mq_notify.return!, nd2_syscall.mq_notify.return!, tp_syscall.mq_notify.return
  { }
  
probe nd1_syscall.mq_notify.return =
        __nd1_syscall.mq_notify.return ?,
        kprobe.function("compat_sys_mq_notify").return ?
{
	@_SYSCALL_MQ_NOTIFY_NAME
	@SYSC_RETVALSTR(returnval())
}
probe __nd1_syscall.mq_notify.return = kprobe.function("sys_mq_notify").return
{
        @__syscall_gate(@const("__NR_mq_notify"))
}

/* kernel 4.17+ */
probe nd2_syscall.mq_notify.return =
	kprobe.function(@arch_syscall_prefix "sys_mq_notify").return ?,
	kprobe.function(@arch_syscall_prefix "compat_sys_mq_notify").return ?
{
	@_SYSCALL_MQ_NOTIFY_NAME
	@SYSC_RETVALSTR(returnval())
}
 
/* kernel 3.5+, but undesirable because it affects all syscalls */
probe tp_syscall.mq_notify.return = kernel.trace("sys_exit")
{
	__set_syscall_pt_regs($regs)
	@__syscall_compat_gate(@const("__NR_mq_notify"), @const("__NR_compat_mq_notify"))
	@_SYSCALL_MQ_NOTIFY_NAME
	@SYSC_RETVALSTR($ret)
}
