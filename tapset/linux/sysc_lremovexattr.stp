# lremovexattr _______________________________________________
# long sys_lremovexattr(char __user *path, char __user *name)
#

@define _SYSCALL_LREMOVEXATTR_NAME
%(
	name = "lremovexattr"
%)

@define _SYSCALL_LREMOVEXATTR_ARGSTR
%(
	argstr = sprintf("%s, %s", path, name_str)
%)

@define _SYSCALL_LREMOVEXATTR_REGARGS
%(
	path_uaddr = pointer_arg(1)
	path = user_string_quoted(path_uaddr)
	name_uaddr = pointer_arg(2)
# 'name2' should have been 'name_str'.  Deprecate the old name.
%(systemtap_v <= "1.4" %?
	name2 = user_string_quoted(name_uaddr)
%)
	name_str = user_string_quoted(name_uaddr)
%)

probe syscall.lremovexattr = dw_syscall.lremovexattr !, nd_syscall.lremovexattr ? {}
probe syscall.lremovexattr.return = dw_syscall.lremovexattr.return !, nd_syscall.lremovexattr.return ? {}

# dw_lremovexattr _____________________________________________________

probe dw_syscall.lremovexattr = kernel.function("sys_lremovexattr").call
{
	@_SYSCALL_LREMOVEXATTR_NAME
	name_uaddr = $name
# 'name2' should have been 'name_str'.  Deprecate the old name.
%(systemtap_v <= "1.4" %?
	name2 = user_string_quoted($name)
%)
	name_str = user_string_quoted($name)
	path_uaddr = @choose_defined($pathname, $path)
	path = user_string_quoted(@choose_defined($pathname, $path))
	@_SYSCALL_LREMOVEXATTR_ARGSTR
}
probe dw_syscall.lremovexattr.return = kernel.function("sys_lremovexattr").return
{
	@_SYSCALL_LREMOVEXATTR_NAME
	@SYSC_RETVALSTR($return)
}

# nd_lremovexattr _____________________________________________________

probe nd_syscall.lremovexattr = nd1_syscall.lremovexattr!, nd2_syscall.lremovexattr!, tp_syscall.lremovexattr
  { }

probe nd1_syscall.lremovexattr = kprobe.function("sys_lremovexattr") ?
{
	@_SYSCALL_LREMOVEXATTR_NAME
	asmlinkage()
	@_SYSCALL_LREMOVEXATTR_REGARGS
	@_SYSCALL_LREMOVEXATTR_ARGSTR
}

/* kernel 4.17+ */
probe nd2_syscall.lremovexattr = kprobe.function(@arch_syscall_prefix "sys_lremovexattr") ?
{
	__set_syscall_pt_regs(pointer_arg(1))
	@_SYSCALL_LREMOVEXATTR_NAME
	@_SYSCALL_LREMOVEXATTR_REGARGS
	@_SYSCALL_LREMOVEXATTR_ARGSTR
}

/* kernel 3.5+, but undesirable because it affects all syscalls */
probe tp_syscall.lremovexattr = kernel.trace("sys_enter")
{
	__set_syscall_pt_regs($regs)
	@__syscall_compat_gate(@const("__NR_lremovexattr"), @const("__NR_compat_lremovexattr"))
	@_SYSCALL_LREMOVEXATTR_NAME
	@_SYSCALL_LREMOVEXATTR_REGARGS
	@_SYSCALL_LREMOVEXATTR_ARGSTR
}

probe nd_syscall.lremovexattr.return = nd1_syscall.lremovexattr.return!, nd2_syscall.lremovexattr.return!, tp_syscall.lremovexattr.return
  { }

probe nd1_syscall.lremovexattr.return = kprobe.function("sys_lremovexattr").return ?
{
	@_SYSCALL_LREMOVEXATTR_NAME
	@SYSC_RETVALSTR(returnval())
}

/* kernel 4.17+ */
probe nd2_syscall.lremovexattr.return = kprobe.function(@arch_syscall_prefix "sys_lremovexattr").return ?
{
	@_SYSCALL_LREMOVEXATTR_NAME
	@SYSC_RETVALSTR(returnval())
}
 
/* kernel 3.5+, but undesirable because it affects all syscalls */
probe tp_syscall.lremovexattr.return = kernel.trace("sys_exit")
{
	__set_syscall_pt_regs($regs)
	@__syscall_compat_gate(@const("__NR_lremovexattr"), @const("__NR_compat_lremovexattr"))
	@_SYSCALL_LREMOVEXATTR_NAME
	@SYSC_RETVALSTR($ret)
}
